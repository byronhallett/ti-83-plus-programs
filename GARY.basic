:" A program called gary, performs tic tac toe
:ClrHome
:Disp "TIC TAC TOE v1.1"
:"W stores winner, set by prgmCHECK later
:0->W
:"L1 stores board state 
:9->dim(L1)
:Fill(0,L1)
:"Draw empty board 
:For(X,1,3)
:For(Y,1,3)
:Output(Y*2,X*2,"-")
:End
:End
:"P stores player turn
:1->P
:Lbl 1
:getKey->K
:If K=0
:Goto 1
:"Check for exit buttons
:If K=102 or K=45 or K=105 or K=22
:Goto 3
:"Otherwise find the grid position of input
:int(K/10)->Y
:K-Y*10->X
:"apply offset to grid (number buttons are at the cenrter bottom of the calc)
:X-1->X
:Y-6->Y
:"Ensure a number button was pressed
:If X<1 or X>3 or Y<1 or Y>3
:Goto 1
:"L stores current cell index in L1
:3(Y-1)+X->L
:"Disallow overwriting over turns
:If L1(L)!=0
:Goto 1
:"For player 1, write 1 to L1 and print X to display
:If P=1
:Then
:Output(Y*2,X*2,"X")
:2->P
:1->L1(L)
:Else
:"For player 2, write 2 to L1 and print O to display
:Output(Y*2,X*2,"O")
:1->P
:2->L1(L)
:End
:"Subroutine will set W if there is a winner or tie
:prgmCHECK
:if W!=0
:Goto 3
:Goto 1
:Lbl 3
:"Output the game outcome
:If W=1
:Output(7,1,"X WINS")
:If W=2
:Output(7,1,"O WINS")
:If W=0
:Output(7,1,"GAME OVER")
:If W=-1
:Output(7,1,"ITS A TIE")

